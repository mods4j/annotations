plugins {
  id 'java-library'
  id 'maven-publish'
  id 'signing'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group = 'io.github.mods4j'
version = '0.2.0'

repositories {
  jcenter()
}

java {
  withSourcesJar()
  withJavadocJar()
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = 'mods4j-annotations'
      from components.java
      versionMapping {
        usage('java-api') {
          fromResolutionOf('runtimeClasspath')
        }
        usage('java-runtime') {
          fromResolutionResult()
        }
      }
      pom {
        name = 'mods4j annotations'
        description = 'mods4j annotations'
        url = 'https://github.com/mods4j/annotations'
        licenses {
          license {
            name = 'MIT License'
            url = 'https://github.com/mods4j/annotations/blob/master/LICENSE'
          }
        }
        developers {
          developer {
            id = 'tprado'
            name = 'Thiago Prado'
            email = 'thiago.gozzi.prado@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:https://github.com/mods4j/annotations.git'
          developerConnection = 'scm:git:git@github.com:mods4j/annotations.git'
          url = 'https://github.com/mods4j/annotations'
        }
      }
    }
  }

  repositories {
    maven {
      def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
      def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
      url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

      credentials {
        username = ossrhUserName
        password = ossrhPassword
      }
    }
  }
}

signing {
  useGpgCmd()
  sign publishing.publications.mavenJava
}
